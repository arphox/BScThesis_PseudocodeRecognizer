-----------------------------------------------------------------------------------------------
	Dinamikus szabályok:
-----------------------------------------------------------------------------------------------

<Program>							::=		"program_kezd" "újsor" <Állítások> "program_vége"
			
<Állítások>							::=		<Állítás> "újsor" <Állítások>
										|	<Állítás> "újsor"
										
<Állítás>							::=		<VáltozóDeklaráció>
										|	<Értékadás>
										|	<IoParancs>
										|	"kilép"
										|	"ha" <LogikaiKifejezés> "akkor" "újsor" <Állítások> "újsor" "különben" "újsor" <Állítások> "újsor" "elágazás_vége"
										|	"ha" <LogikaiKifejezés> "akkor" "újsor" <Állítások> "újsor" "elágazás_vége"
										|	"ciklus_amíg" <LogikaiKifejezés> "újsor" <Állítások> "újsor" "ciklus_vége"

<VáltozóDeklaráció>					::=		<AlapTípus> "azonosító" "=" <NemTömbLétrehozóKifejezés>
										|	<TömbTípus> "azonosító" "=" <TömbLétrehozóKifejezés>

<Értékadás>							::=		"azonosító" "=" <NemTömbLétrehozóKifejezés>
										|	"azonosító" "=" <TömbLétrehozóKifejezés>
										|	"azonosító" "[" <NemTömbLétrehozóKifejezés> "]" "=" <NemTömbLétrehozóKifejezés>

<LogikaiKifejezés>					::=		<NemTömbLétrehozóKifejezés>

<Operandus>							::=		<UnárisOperátor> "azonosító"
										|	<UnárisOperátor> "literál"
										|	"azonosító"
										|	"literál"

<NemTömbLétrehozóKifejezés>			::=		<BinárisKifejezés>
										|	<Operandus>

<TömbLétrehozóKifejezés>			::=		"azonosító"
										|	"létrehoz" "[" <NemTömbLétrehozóKifejezés> "]"

<BinárisKifejezés>					::=		<Operandus> <BinárisOperátor> <Operandus>

<BinárisOperátor>					::=		"="
										|	"=="
										|	"!="
										|	"és"
										|	"vagy"
										|	">"
										|   ">="
										|	"<"
										|	"<="
										|	"+"
										|	"-"
										|	"*"
										|	"/"
										|	"mod"
										|	"."

-----------------------------------------------------------------------------------------------
	Statikus szabályok:
-----------------------------------------------------------------------------------------------

<Típus>								::=		<AlapTípus>
										|	<TömbTípus>

<AlapTípus>							::=		"egész"
										|	"tört"
										|	"szöveg"
										|	"logikai"

<TömbTípus>							::=		"egész tömb"
										|	"tört tömb"
										|	"szöveg tömb"
										|	"logikai tömb"

<IoParancs>							::=		"beolvas" "azonosító"
										|	"kiír" "azonosító"

<BelsőFüggvény>						::=		"egészből_logikaiba"
										|	"törtből_egészbe"
										|	"törtből_logikaiba"
										|	"logikaiból_egészbe"
										|	"logikaiból_törtbe"
										|	"szövegből_egészbe"
										|	"szövegből_törtbe"
										|	"szövegből_logikaiba"

<UnárisOperátor>					::=		"-"
										|	"!"