Itt található az új nyelvtan BNF formában.
Felsorolási szabály, hogy alulról felfelé olvasva a szabályokat
mindig hamarabb történjen a definíció, mint a használat.
A szabályok megadása során csak a jobbrekurzió engedett meg.
-----------------------------------------------------------------------------------------------

<Program>							::=		"program_kezd" "újsor" <Állítások> "program_vége"
			
<Állítások>							::=		<Állítás> "újsor" <Állítások>
										|	<Állítás> "újsor"
										
<Állítás>							::=		<VáltozóDeklaráció>
										|	<VáltozóDefiníció>
										|	<Értékadás>
										|	<IoParancs> "azonosító"
										|	"kilép"
										|	"ha" <LogikaiKifejezés> "akkor" "újsor" <Állítás> "újsor" "különben" "újsor" <Állítás> "újsor" "elágazás_vége"
										|	"ha" <LogikaiKifejezés> "akkor" "újsor" <Állítás> "újsor" "elágazás_vége"
										|	"ciklus_amíg" <LogikaiKifejezés> "újsor" <Állítás> "újsor" "ciklus_vége"
										|	"ciklus" <SzámlálóCiklusInicializáló> "-tól" <LogikaiKifejezés> "-ig" "újsor" <Állítás> "újsor" "ciklus_vége"

<VáltozóDeklaráció>					::=		<AlapVáltozóDeklaráció>
										| 	<TömbVáltozóDeklaráció>

<VáltozóDefiníció>					::=		<Típus> "azonosító"

<AlapVáltozóDeklaráció>				::=		<AlapTípus> "azonosító" "=" <AlapKifejezés>

<TömbVáltozóDeklaráció>				::=		<TömbTípus> "azonosító" "=" <TömbLétrehozóKifejezés>

<Értékadás>							::=		<AlapÉrtékadás>
										|	<TömbÉrtékadás>

<AlapÉrtékadás>						::=		"azonosító" "=" <AlapKifejezés>
										|	<TömbElemElérés> "=" <AlapKifejezés>

<TömbElemElérés>					::=		"azonosító" "[" "azonosító" "]"
										|	"azonosító" "[" <AlapKifejezés> "]"

<TömbÉrtékadás>						::=		"azonosító" "=" <TömbLétrehozóKifejezés>

<SzámlálóCiklusInicializáló>		::=		<AlapVáltozóDeklaráció>
										|	<AlapÉrtékadás>

<LogikaiKifejezés>					::=		<Kifejezés>

<Kifejezés>							::=		<AlapKifejezés>
										|	<TömbLétrehozóKifejezés>

<AlapKifejezés>						::=		"literál"
										|	"azonosító"

<TömbLétrehozóKifejezés>			::=		"azonosító"
										|	"létrehoz" "[" <AlapKifejezés> "]"



-----------------------------------------------------------------------------------------------
	Statikus szabályok:
-----------------------------------------------------------------------------------------------

<Típus>								::=		<AlapTípus>
										|	<TömbTípus>

<AlapTípus>							::=		"egész"
										|	"tört"
										|	"szöveg"
										|	"logikai"

<TömbTípus>							::=		"egész tömb"
										|	"tört tömb"
										|	"szöveg tömb"
										|	"logikai tömb"

<IoParancs>							::=		"beolvas"
										|	"kiír"

<BelsőFüggvény>						::=		"egészből_logikaiba"
										|	"törtből_egészbe"
										|	"törtből_logikaiba"
										|	"logikaiból_egészbe"
										|	"logikaiból_törtbe"
										|	"szövegből_egészbe"
										|	"szövegből_törtbe"
										|	"szövegből_logikaiba"